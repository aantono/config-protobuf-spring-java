println """\
Welcome to Gradle $gradle.gradleVersion - http://www.gradle.org
Gradle home is set to: $gradle.gradleHomeDir
Gradle user directory is set to: $gradle.gradleUserHomeDir

Base directory: $projectDir
Running script ${relativePath(buildFile)}
"""

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

apply plugin: ws.antonov.gradle.plugins.protobuf.ProtobufPlugin
apply plugin: de.huxhorn.gradle.pgp.PgpPlugin

version = '0.1'
group = 'ws.antonov.config'
httpPort = 8585

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.huxhorn.gradle:de.huxhorn.gradle.pgp-plugin:0.0.3'
        classpath 'ws.antonov.gradle.plugins:gradle-plugin-protobuf:0.4'
    }
}

// local 'lib' directory should have protobuf-format-java-1.2.jar and protobuf-java-2.3.0.jar
repositories {
  mavenCentral()
  //flatDir name: 'localRepository', dirs: 'lib'
}

configurations {
  compile.transitive = true
  testCompile {
    transitive = true
    extendsFrom compile
  }
}

dependencies {
  compile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
  compile group: 'com.google.protobuf', name: 'protobuf-java', version: '2.4.1'
  compile group: 'com.googlecode.protobuf-java-format', name: 'protobuf-java-format', version: '1.2'
  compile group: 'org.springframework', name: 'spring-core', version: '3.0.2.RELEASE'
  compile group: 'org.springframework', name: 'spring-asm', version: '3.0.2.RELEASE'
  compile group: 'cglib', name: 'cglib-nodep', version: '2.2.2'
  compile group: 'org.springframework', name: 'spring-webmvc', version: '3.0.2.RELEASE'
  compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.5.0-M0'
  //compile group: 'org.apache.mina', name: 'mina-core', version: '2.0.0-M4'
  compile group: 'commons-httpclient', name: 'commons-httpclient', version: '3.1'

  testCompile "junit:junit:3.8.2"
  testCompile group: "org.springframework", name: "spring-test", version: "3.0.2.RELEASE"
}

pgp {
    secretKeyRingFile = new File("${System.properties['user.home']}/.gnupg/secring.gpg")
    // keyId: specify with -PpgpKeyId=
    // password: specify with -PpgpPassword=
}

task sourcesJar(type: Jar, dependsOn:classes) {
     classifier = 'sources'
     from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
     classifier = 'javadoc'
     from javadoc.destinationDir
}

task listCompileJars << {
    configurations.compile.each { File file -> println file.name }
}

task listTestJars << {
    configurations.testCompile.each { File file -> println file.name }
}

task wrapper(type: Wrapper) {
    gradleVersion = '0.9.2'
}

artifacts {
     archives sourcesJar
     archives javadocJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "http://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: System.getProperty('nexusUser'), password: System.getProperty('nexusPassword'))
      }
      pom.project {
        name project.name
        description "Configuration abstraction framework using Google Protocol Buffers, Spring and varous formatters"
        url "https://github.com/aantono/config-protobuf-spring-java"
        licenses {
          license {
            name "New BSD License"
            url "http://www.opensource.org/licenses/bsd-license.php"
          }
        }
        developers {
          developer {
            id "aantono"
            name "Alex Antonov"
            email "alex@antonov.ws"
          }
        }
        scm {
          connection "scm:git:git://github.com/aantono/config-protobuf-spring-java.git"
          developerConnection "scm:git:git@github.com:aantono/config-protobuf-spring-java.git"
          url "https://github.com/aantono/config-protobuf-spring-java"
        }
      }
    }
  }
}
